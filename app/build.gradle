apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.ljstudio.android.loveday"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 9
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../loveday.jks")
            keyAlias "loveday"
            storePassword "lj123456"
            keyPassword "lj123456789"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"

            versionNameSuffix "-release"
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    //  apk名称为 LoveDay-V1.0.0_2015-12-31_online.apk
                    def fileName = "LoveDay-V${versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    productFlavors {
        online {
            manifestPlaceholders = [APK_CHANNEL: "online"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://online.domain.com/\""
            versionName getReleaseVersionName()
        }

        admin {
            manifestPlaceholders = [APK_CHANNEL: "admin"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://admin.domain.com/\""
            versionName getDebugVersionName()
        }

        atest {
            manifestPlaceholders = [APK_CHANNEL: "atest"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://offline.domain.com/\""
            versionName getDebugVersionName()
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

greendao {
    schemaVersion 2
    daoPackage 'com.ljstudio.android.loveday.greendao'
    targetGenDir 'src/main/java'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'org.greenrobot:greendao:3.2.2'

    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'

    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'

    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.google.code.gson:gson:2.8.0'

    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.15'
    compile 'com.github.nekocode:TriangulationDrawable:1.1'
    compile 'com.github.lzyzsd.randomcolor:library:1.0.0'
    compile 'com.github.GrenderG:Toasty:1.2.3'

    compile 'com.afollestad.material-dialogs:core:0.9.4.4'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.4'

    compile "io.reactivex.rxjava2:rxjava:2.0.7"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
}
